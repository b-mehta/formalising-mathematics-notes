/-
Copyright (c) 2025 Yunkai Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yunkai Zhang
-/
import Mathlib.Tactic
import Mathlib.CategoryTheory.Limits.Shapes.IsTerminal

/-!
# Lambek's Lemma

This file formalises Lambek's Lemma.

Lambek's Lemma states that for an endofunctor `F`, if an F-algebra is initial,
then its structural map is an isomorphism.

The proof follows categorical axioms, demonstrating the existence of inverse morphisms
using the universal properties of initial/terminal objects.

## Main declarations

* `FAlgebra`: Structure for an F-algebra with carrier object and structural morphism
* `AlgebraHom`: The homomorphisms between F-algebras
* `FAlgebra.Initial.lambek`: The main theorem stating that initial F-algebras have isomorphic structural maps

## Implementation notes

The implementation builds upon mathlib's category theory foundations, particularly using:
* Category type classes and structures
* Initial object definitions from limits
* Isomorphism type classes

We define the category of F-algebras by providing appropriate morphism structures
and proving the categorical axioms.

## References

* [S. Awodey, *Category Theory*][awodey2010]
* [nLab, *Initial Algebra of an Endofunctor*][nlab]
* [A. Kissinger and J. Rot, *Colecture 1: Algebras, algebraic data types, and recursion*][kissinger2016]
-/

set_option autoImplicit false

namespace CategoryTheory

universe u v

variable {C : Type u} [Category.{v} C]

-- following the conventional notation
local notation:80 g " ‚äö " f:80 => CategoryTheory.CategoryStruct.comp f g

variable {F : C ‚•§ C}

structure FAlgebra (F : C ‚•§ C) where
  /-- carrier -/
  carrier : C
  /-- the arrow for the structural morphism -/
  mor : F.obj carrier ‚ü∂ carrier

namespace FAlgebra

/-- Define that all F-Algebra form a category.
This include components:
* homomorphisms: `h : (A, Œ±) ‚ü∂ (B, Œ≤)` is essentially an arrow `h : A ‚ü∂ B`
  such that `h ‚àò Œ± = Œ≤ ‚àò F (h)`
* identities: identity arrows

```
         F h
   F A -----> F B
    |         |
  Œ± |         | Œ≤
    V         V
    A  -----> B
        h
```
-/

@[ext]
structure AlgebraHom (A B : FAlgebra F) where
  -- mathching carrier
  h : A.carrier ‚ü∂ B.carrier
  -- commutative conidition
  condition : h ‚äö A.mor = B.mor ‚äö (F.map h)

variable (A : FAlgebra F) {A' B' C': FAlgebra F}


namespace AlgebraHom

def id : AlgebraHom A A where
  h := ùüô _
  condition := by
    aesop

-- this is for proof 2 in the next def
attribute [reassoc] AlgebraHom.condition

-- Composition of homomorphisms between algebras
-- BM: this one needs a docstring
-- notation is /-- docstring -/
def comp (m1: AlgebraHom A' B') (m2: AlgebraHom B' C') : AlgebraHom A' C' where
  h := m2.h ‚äö m1.h
  condition := by
    -- original
    -- simp [Functor.map_comp]
    -- rw [‚Üê m2.condition]
    -- simp [‚Üê Category.assoc]
    -- rw [m1.condition]
    -- this has a nonterminal simp!

    -- proof 1
    -- rw [F.map_comp, Category.assoc, ‚Üê m2.condition, ‚Üê Category.assoc, m1.condition,
    --   Category.assoc]
    -- you can do this just with rewrites

    -- proof 2
    simp only [Functor.map_comp, Category.assoc]
    rw [‚Üê m2.condition, m1.condition_assoc]
    -- any lemma tagged with reassoc will generate an "associated" version
    -- that means a lemma f ‚äö g = h will get a new version which is
    -- f ‚äö (g ‚äö p) = h ‚äö p
    -- and call it "{original_name}_assoc"
    -- the idea is that simp will turn a chain of compositions into something of shape
    -- a ‚äö (b ‚äö (c ‚äö ...)), and so in f ‚äö (g ‚äö p) the original f ‚äö g = ... lemma won't match
    -- but the _assoc lemma will match
    -- making and using _assoc lemmas should mean you never need to do rw [‚Üê assoc]

end AlgebraHom

instance (F : C ‚•§ C) : CategoryStruct (FAlgebra F) where
  Hom := AlgebraHom
  id := AlgebraHom.id -- (X : FAlgebra F) ‚Üí X ‚ü∂ X
  comp := @AlgebraHom.comp _ _ _ -- {X Y Z : FAlgebra F} ‚Üí (X ‚ü∂ Y) ‚Üí (Y ‚ü∂ Z) ‚Üí (X ‚ü∂ Z)

@[ext]
lemma ext {A B : FAlgebra F} {f g : A ‚ü∂ B} (w : f.h = g.h) : f = g :=
  AlgebraHom.ext w

theorem comp_distr {f : B' ‚ü∂ C'} {g : A' ‚ü∂ B'} : (f ‚äö g).h = f.h ‚äö g.h := by
  rfl

theorem id_distr {A : FAlgebra F}: (ùüô _ : A ‚ü∂ A).h = ùüô A.carrier := by
  rfl


-- We need to show: All F-Algebras form a category
instance (F : C ‚•§ C) : Category (FAlgebra F) where
  -- use `where` rather than `:= {}`
  -- (some of the example sheets got this wrong, my bad!)
  --  ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), ùüô X ‚â´ f = f
  -- you can put `‚àÄ x,` arguments before the `:=`
  -- but if they were `‚àÄ {x},` then it doesn't like that
  -- so use {X Y f} and it's fine again
  id_comp {X Y} f := by
    ext
    rw [comp_distr, id_distr, Category.id_comp]
  -- ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), f ‚â´ ùüô Y = f
  comp_id {X Y} f := by
    ext
    rw [comp_distr, id_distr, Category.comp_id]
  -- Composition in a category is associative.
  assoc {W X Y Z} f g h := by
    ext
    simp [comp_distr]

/-
  The idea of the proof:

  ```
         F f           F (i)
   F I -----> F (F I) -------> F (I)
    |         |                 |
  i |         | F(i)            | i
    V         V                 V
    I  -----> F I ------------> I
        f             i
```
Given `I` is an initial object in the category of algebras on endofunctors:

BM: I'm being pedantic here, but whatever.
Initial objects / terminal objects are unique up to unique isomorphism, but they're not actually
unique! eg in the category of sets, {1} and {2} aren't the same object. They're isomorphic, and
there's a unique isomorphism between them, but they're not equal.
This remains true in Lean category theory

`f` is the unique arrow from Algebra of (F I --i--> I)
to Algebra of (F (F I) --F (i)--> F I)

so `i ‚äö f` constitutes an arrow from I to I.

However, by I being an initial object, there is one unique arrow from I to I,
which is the identity arrow. Therefore, `i ‚äö f = id_I`

With this in mind, as the left square commutes: we have

```
f ‚äö i = F (i) ‚äö F (f)
      = F (i ‚äö f)
      = F (id_I)
      = id_(F (I))
```

-/
namespace Initial

variable {I : FAlgebra F} -- The initial object
-- If I do
-- #where
-- (with your version, at this point in the file) then Lean tells me that it has no idea what the
-- type of `I` should be -- but presumably you want it to be the object in FAlgebra F which you're
-- gonna say later is initial
-- so be explicit about this

-- docstring notation again
/--
The initial algebra.
`abbrev` is used instead of `def` to make it possible to be unfolded
-/
abbrev i_to_fi (hInit : Limits.IsInitial I) :=
  hInit.to ‚ü®F.obj I.carrier, F.map I.mor‚ü©
-- giving the type of I explicitly means I don't need the @ in hInit

-- it might have been sensible to make the object
-- fi : FAlgebra F := ‚ü®F.obj I.carrier, F.map I.mor‚ü©
-- then this would just be `hInit.to fi`

/--
Construct the homomorphism from Algebra (I, i) to (I, i),
which is formed by composing the homomorphism from (I, i) to (F(I), F(i))
and the homomorphism from (F(I), F(i)) to (I, i)
-/
@[simps] -- this is making a lemma called i_to_i_alg_hom_h proved by rfl and marking it simp
-- change it to simps? to see it
def i_to_i_alg_hom (hInit : Limits.IsInitial I) : I ‚ü∂ I where
  h := I.mor ‚äö (i_to_fi hInit).h
  condition := by
    rw [‚Üê Category.assoc, F.map_comp, i_to_fi, ‚Üê AlgebraHom.condition]

/- i ‚äö f = id_I -/
lemma is_inv_1 (hInit : Limits.IsInitial I) :
    I.mor ‚äö (i_to_fi hInit).h = ùüô I.carrier := by
  have h1 : i_to_i_alg_hom hInit = ùüô I := hInit.hom_ext _ (ùüô I)
  have h2 : (i_to_i_alg_hom hInit).h = ùüô I.carrier :=
    congr_arg AlgebraHom.h h1
    -- another (weirder looking) option is
    -- congr(($h1).h)
    -- or
    -- congr(AlgebraHom.h ($h1))
  rw [‚Üê h2]
  simp -- this works because of the @[simps] I added to `i_to_i_alg_hom`
  -- more broadly, `unfold` is often a sign you have a missing rfl lemma

/-- f ‚äö I = id_F(I) -/
lemma is_inv_2 (hInit : @Limits.IsInitial (FAlgebra F) _ I) :
    (i_to_fi hInit).h ‚äö I.mor = ùüô (F.obj I.carrier) := by
  -- unfold i_to_fi
  -- not needed!
  rw [(i_to_fi hInit).condition, ‚Üê F.map_id, ‚Üê F.map_comp]
  congr
  apply is_inv_1 hInit

/--
Lambek's Lemma:
if Algebra I : F (i) --i--> I is an initial F-algebra,
Then i is an isomorphism, with F (I) ‚âÖ I
-/
theorem lambek (hInit : @Limits.IsInitial (FAlgebra F) _ I) : IsIso I.mor where
  out := ‚ü® (i_to_fi hInit).h, is_inv_2 hInit , is_inv_1 hInit ‚ü©

-- BM: I stopped here
-- just dualise the above comments for FCoalgebra :)
end Initial

end FAlgebra
